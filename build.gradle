import org.asciidoctor.gradle.AsciidoctorTask

plugins {
    id "org.asciidoctor.convert" version "1.5.6"
}

apply plugin: 'groovy'

dependencies {
    testCompile(
        'junit:junit:4.12',
        'org.codehaus.groovy:groovy-all:2.4.9',
        'org.spockframework:spock-core:1.1-groovy-2.4'
    )
    asciidoctor 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
}

asciidoctorj {
    version = '1.5.4'
}

ext {
    currentDate = new Date().format("d. MMM yyyy")
}

asciidoctor {
    attributes = [
        doctype: 'book',
        icons: 'font',
        sectlink: true,
        sectanchors: true,
        numbered: true,
        'source-highlighter': 'coderay',
        'currentDate': "$currentDate"
   ]
}

tasks.withType(AsciidoctorTask) { docTask ->

    attributes \
          'source-highlighter': 'coderay',
          'toc': 'right',
          'icons': 'font',
          'sourceDir': "$projectDir/src/"

    // good to see what the build is doing...
    logDocuments = true
}

task generatePDF (
    type: AsciidoctorTask,
    description: 'use pdf as asciidoc backend') {

    backends = ['pdf']
}

defaultTasks 'clean', 'asciidoctor'